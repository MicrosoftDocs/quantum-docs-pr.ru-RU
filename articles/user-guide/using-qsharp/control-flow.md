---
title: 'Поток управления в Q #'
description: Циклы, условные выражения и т. д.
author: gillenhaalb
ms.author: a-gibec@microsoft.com
ms.date: 03/05/2020
ms.topic: article
uid: microsoft.quantum.guide.controlflow
ms.openlocfilehash: c534e016fcb8b50e66c11ca29c253ba0512acc6e
ms.sourcegitcommit: 2317473fdf2b80de58db0f43b9fcfb57f56aefff
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/15/2020
ms.locfileid: "83430957"
---
# <a name="control-flow-in-q"></a>Поток управления в Q #

Внутри операции или функции каждая инструкция выполняется по порядку, аналогично наиболее распространенным императивным классическим языкам.
Однако этот поток управления можно изменить тремя разными способами:

- `if`инструкции
- `for`бираются
- `repeat`-`until`бираются

Мы относимся к обсуждению второго варианта [ниже](#repeat-until-success-loop).
`if` `for` Однако конструкции потока управления и имеют привычный смысл для большинства классических языков программирования.

Важно, `for` что циклы и `if` операторы можно даже использовать в операциях, для которых автоматически создаются специализации. В этом случае смежная часть `for` цикла обращается к направлению и принимает смежную часть каждой итерации.
Это соответствует принципу "обувь-and-Socks": Если вы хотите отменить размещение по протоколу SOCKS, а затем обувь, необходимо отменить помещение обувь, а затем отменить последующую операцию по протоколу SOCKS.
Это решение работает менее эффективно, и вы можете использовать протокол Socks, пока вы по-прежнему людьмие обувь!

## <a name="if-else-if-else"></a>If, else — if, else

`if`Оператор поддерживает условное выполнение.
Он состоит из ключевого слова `if` , открывающей скобки `(` , логического выражения, закрывающей скобки `)` и блока операторов (блок _then_ ).
За этим может следовать любое число предложений else-if, каждое из которых состоит из ключевого слова `elif` , открывающей скобки `(` , логического выражения, закрывающей скобки `)` и блока операторов ( _Else-If_ ).
Наконец, оператор может при необходимости завершиться предложением else, которое состоит из ключевого слова, `else` за которым следует другой блок операторов (блок _else_ ).

`if`Условие вычисляется, и, если оно равно true, выполняется блок then.
Если условие имеет значение false, то проверяется первое условие else-if; Если значение равно true, выполняется блок Else-If.
В противном случае проверяется второй блок-If, а затем третий и т. д., пока не будет обнаружено предложение с истинным условием или отсутствуют другие предложения Else-If.
Если исходное условие If и все предложения Else-If имеют значение false, то блок Else выполняется, если он был предоставлен.

Обратите внимание, что любой блок выполняется в своей собственной области.
Привязки, сделанные внутри `if` `elif` блока, или, `else` не видны после завершения.

Например, примененная к объекту директива

```qsharp
if (result == One) {
    X(target);
    let n = 1;
    // n is bound
} 
// n is not bound
```
or
```qsharp
if (i == 1) {
    X(target);
    let n = 1;
} elif (i == 2) {
    Y(target);
    let m = n + 1; // would cause an error, because n is no longer bound
} else {
    Z(target);
}
```

## <a name="for-loop"></a>Цикл For

`for`Инструкция поддерживает итерацию по диапазону целых чисел или к массиву.
Оператор состоит из ключевого слова `for` , открывающей скобки `(` , за которой следует символ или кортеж символов, ключевое слово `in` , выражение типа `Range` или массива, закрывающая круглая скобка `)` и блок операторов.

Блок операторов (тело цикла) выполняется повторно с определенными символами (переменными цикла), привязанными к каждому значению в диапазоне или массиве.
Обратите внимание, что если выражение диапазона принимает пустой диапазон или массив, тело не будет выполняться вообще.
Выражение полностью вычисляется перед входом в цикл и не будет изменяться во время выполнения цикла.

Переменная цикла привязывается к каждому входу в тело цикла и не привязана к концу тела.
В частности, переменная цикла не привязана после завершения цикла for.
Привязка объявленных символов является неизменяемой и соответствует тем же правилам, что и другие привязки переменных. 

В некоторых примерах допустим `qubits` является регистром Кубитс (т. е. типа `Qubit[]` ), 

```qsharp
// ...
for (qubit in qubits) {  // iterate over each qubit value in the array qubits
    H(qubit);
}
// 'qubit' is no longer bound

mutable results = new (Int, Results)[Length(qubits)];
for (index in 0 .. Length(qubits) - 1) {  // iterates over the integers in the Range 0 .. (Length(qubits) - 1)
    set results w/= index <- (index, M(qubits[index])); // measure each qubit, updates the tuple value in the results array that at index 
}

mutable accumulated = 0;
for ((index, measured) in results) { // iterates over the tuple values in results
    if (measured == One) {
        set accumulated += 1 <<< index;
    }
}
```
Обратите внимание, что в конце мы использовали бинарный оператор арифметического сдвига влево, `<<<` который можно найти в [числовых выражениях](xref:microsoft.quantum.guide.expressions#numeric-expressions) .


## <a name="repeat-until-success-loop"></a>Цикл "повторить" до "успешно"

Язык Q # позволяет классического потока управления зависеть от результатов измерения Кубитс.
Эта возможность, в свою очередь, позволяет реализовать мощные мини-приложения вероятностная, которые могут снизить вычислительные затраты для реализации унитариес.
В качестве примера можно легко реализовать так называемые шаблоны *повторения до успешного выполнения* (RUS) в Q #.
Эти шаблоны RUS являются вероятностная программами, которые имеют *ожидаемую* низкую стоимость в терминах простейших шлюзов, но для которых реальная стоимость зависит от фактического запуска и фактического перехода различных вариантов ветвления.

Чтобы упростить шаблоны повторения до успешного завершения (RUS), Q # поддерживает конструкции

```qsharp
repeat {
    // do stuff
}
until (expression)
fixup {
    // do other stuff
}
```

где `expression` — любое допустимое выражение, результатом вычисления которого является значение типа `Bool` .
Выполняется тело цикла, а затем вычисляется условие.
Если условие истинно, инструкция завершается; в противном случае выполняется адресная привязка и выполняется повторная инструкция, начиная с тела цикла.

Все три части цикла повтора и до (текст, тест и адресная привязка) обрабатываются как единая область *для каждого повторения*, поэтому символы, привязанные к тексту, доступны в тесте и в адресной привязке.
Однако завершение выполнения адресной привязки завершает область действия инструкции, чтобы привязки символов, сделанные во время тела или исправления, были недоступны при последующих повторах.

Кроме того, `fixup` оператор часто полезен, но не всегда нужен.
В случаях, когда это не требуется, конструкция
```qsharp
repeat {
    // do stuff
}
until (expression);
```
также является допустимым шаблоном RUS.

В нижней части этой страницы представлены некоторые [примеры циклов RUS](#repeat-until-success-examples).

> [!TIP]   
> Избегайте использования циклов повторения до успешного выполнения в функциях. Соответствующие функциональные возможности обеспечиваются циклами while в функциях. 

## <a name="while-loop"></a>Цикл while

Шаблоны Repeat-Until-Success имеют очень много времени. Они широко используются в определенных классах тактовых алгоритмов, поэтому выделенная конструкция языка в Q #. Однако циклы, которые нарушаются в зависимости от условия и длина выполнения которого во время компиляции неизвестны, должны обрабатываться с определенной осторожностью в среде выполнения такта. Их использование в функциях с другой стороны не является проблемой, поскольку в них содержится только код, который будет выполняться на обычном (не такте) оборудовании. 

Таким образом, Q # поддерживает использование циклов while только внутри функций. `while`Оператор состоит из ключевого слова `while` , открывающей скобки `(` , условия (т. е. логического выражения), закрывающей скобки `)` и блока операторов.
Блок операторов (тело цикла) выполняется при условии, что условие принимает значение `true` .

```qsharp
// ...
mutable (item, index) = (-1, 0);
while (index < Length(arr) && item < 0) { 
    set item = arr[index];
    set index += 1;
}
```


## <a name="return-statement"></a>Оператор Return

Оператор Return завершает выполнение операции или функции и возвращает значение вызывающему объекту.
Он состоит из ключевого слова `return` , за которым следует выражение соответствующего типа и завершающая точка с запятой.

Вызываемый метод, возвращающий пустой кортеж, не `()` требует наличия оператора return.
Если требуется ранний выход, `return ()` в этом случае может использоваться.
Для вызова, возвращающего любой другой тип, требуется завершающий оператор return.

В операции отсутствует максимальное число инструкций Return.
Компилятор может выдать предупреждение, если операторы следуют за оператором Return в блоке.

Например, примененная к объекту директива
```qsharp
return 1;
```
or
```qsharp
return ();
```
or
```qsharp
return (results, qubits);
```

## <a name="fail-statement"></a>Оператор Fail

Инструкция Fail завершает выполнение операции и возвращает вызывающему объекту значение ошибки.
Он состоит из ключевого слова `fail` , за которым следует строка и завершающая точка с запятой.
Строка возвращается в классический драйвер в качестве сообщения об ошибке.

Количество инструкций Fail в операции не ограничено.
Компилятор может выдать предупреждение, если операторы следуют за оператором Fail в блоке.

Например, примененная к объекту директива
```qsharp
fail $"Impossible state reached";
```
или с помощью [интерполяции строк](xref:microsoft.quantum.guide.expressions#interpolated-strings),
```qsharp
fail $"Syndrome {syn} is incorrect";
```

## <a name="repeat-until-success-examples"></a>Примеры повторения до успешного выполнения

### <a name="rus-pattern-for-single-qubit-rotation-about-an-irrational-axis"></a>Шаблон RUS для одного кубит вращения по оси иррациональная 

В типичном варианте использования следующая операция Q # реализует поворот вокруг оси иррациональная $ (I + 2i Z)/\скрт {5} $ в БЛОЧ Sphere. Это достигается с помощью известного шаблона RUS:

```qsharp
operation ApplyVRotationUsingRUS(qubit : Qubit) : Unit {
    using (controls = Qubit[2]) {
        ApplyToEachA(H, controls);
        mutable finished = false;
        repeat {
            Controlled X(controls, qubit);
            S(qubit);
            Controlled X(controls, qubit);
            Z(qubit);
        }
        until (finished)
        fixup {
            if (MeasureIfAllQubitsAreZero(controls, PauliX)) {
                set finished = true;
            }
        }
    }
}
```

### <a name="rus-loop-with-mutable-variable-in-scope"></a>Цикл RUS с изменяемой переменной в области видимости

В этом примере показано использование изменяемой переменной `finished` , которая находится в области действия всего цикла Repeat-Until-unадресной привязки и инициализируется перед циклом и обновляется на шаге исправления.

```qsharp
mutable iter = 1;
repeat {
    ProbabilisticCircuit(qubits);
    let success = ComputeSuccessIndicator(qubits);
}
until (success || iter > maxIter)
fixup {
    iter += 1;
    ComputeCorrection(qubits);
}
```

### <a name="rus-without-fixup"></a>RUS без`fixup`

Например, следующий код представляет собой цепь вероятностная, которая реализует важный шлюз ротации $V _3 = (\болдоне + 2 i Z)/\скрт {5} $ с использованием `H` шлюзов и `T` .
Цикл завершается в среднем на $ \фрак {8} {5} $.
Дополнительные сведения см. в разделе [*Повтор-Until-Success: недетерминированная декомпозиция одного кубит унитариес*](https://arxiv.org/abs/1311.1074) (Паетзникк и своре, 2014).

```qsharp
using (qubit = Qubit()) {
    repeat {
        H(qubit);
        T(qubit);
        CNOT(target, qubit);
        H(qubit);
        Adjoint T(qubit);
        H(qubit);
        T(qubit);
        H(qubit);
        CNOT(target, qubit);
        T(qubit);
        Z(target);
        H(qubit);
        let result = M(qubit);
    } until (result == Zero);
}
```

### <a name="rus-to-prepare-a-quantum-state"></a>RUS для подготовки состояния такта

Наконец, мы рассмотрим пример шаблона RUS для подготовки состояния такта $ \фрак {1} {\скрт {3} } \лефт (\скрт {2} \кет {0} + \кет {1} \ригхт) $, начиная с $ \кет{+} $ State.
См. также [Пример модульного тестирования, поставляемый со стандартной библиотекой](https://github.com/microsoft/Quantum/blob/master/samples/diagnostics/unit-testing/RepeatUntilSuccessCircuits.qs):

```qsharp
operation PrepareStateUsingRUS(target : Qubit) : Unit {
    using (auxiliary = Qubit()) {
        H(auxiliary);
        repeat {
            // We expect the target and auxiliary qubits to each be in
            // the |+> state.
            AssertProb(
                [PauliX], [target], Zero, 1.0,
                "target qubit should be in the |+> state", 1e-10 );
            AssertProb(
                [PauliX], [auxiliary], Zero, 1.0,
                "auxiliary qubit should be in the |+> state", 1e-10 );

            Adjoint T(auxiliary);
            CNOT(target, auxiliary);
            T(auxiliary);

            // The probability of measuring |+> state on the auxiliary qubit
            // is 3/4.
            AssertProb(
                [PauliX], [auxiliary], Zero, 3. / 4.,
                "Error: the probability to measure |+> in the first
                auxiliary must be 3/4",
                1e-10);

            // If we get the measurement outcome Zero, we prepare the
            // required state.
            let outcome = Measure([PauliX], [auxiliary]);
        }
        until (outcome == Zero)
        fixup {
            // Bring the auxiliary and target qubits back to |+> state.
            if (outcome == One) {
                Z(auxiliary);
                X(target);
                H(target);
            }
        }
        // Return the auxiliary qubit back to the Zero state.
        H(auxiliary);
    }
}
```

Важные программные функции, показанные в этой операции, — более сложная `fixup` часть цикла, которая включает в себя операции обработки тактов, а также использование `AssertProb` инструкций для определения вероятности измерения состояния такта в определенных точках программы.
Дополнительные сведения об операциях и см. в разделе [тестирование и отладка](xref:microsoft.quantum.guide.testingdebugging) [`Assert`](xref:microsoft.quantum.intrinsic.assert) [`AssertProb`](xref:microsoft.quantum.intrinsic.assertprob) .


## <a name="whats-next"></a>Дальнейшая работа
Сведения о [тестировании и отладке](xref:microsoft.quantum.guide.testingdebugging) в Q #.